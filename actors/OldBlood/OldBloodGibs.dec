//===========================================================================
//
// OldBlood
// Written by Nash Muhandes
// License: MIT
//
//===========================================================================

Actor OldBloodGib
{
	Radius 2
	Height 8
	Gravity 0.43
	Speed 7
	Mass 1
	BounceFactor 0.4
	Decal "BloodSplat"
	+MISSILE
	+DOOMBOUNCE
	+FLOORCLIP
	+DONTSPLASH
	+NOTELEPORT
	+FORCEXYBILLBOARD
	+THRUACTORS

	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Jump(128, 2)
			TNT1 A 0 A_Jump(256, "Spawn2")
			TNT1 A 0 A_Jump(256, "SpawnFail")
			Stop
		Spawn2:
			TNT1 A 0 A_Jump(128, 2)
			TNT1 A 0 A_SetScale(ScaleX * -1.0, ScaleY)
			TNT1 A 0 A_Jump(256, "Spawn3")
			Stop
		Spawn4:
			"####" ABCDEFGH 4
			Loop
		Death:
			"####" "#" 0 ACS_NamedExecuteWithResult("OldBloodAddToQueue", 0)
			"####" "#" 0 A_SpawnItemEx("OldBloodFX1")
			"####" I 35
			"####" "#" 0 A_ChangeFlag("NoInteraction", 1)
			"####" "#" 0 A_ChangeFlag("MoveWithSector", 1)
			"####" "#" -1
			Stop
		SpawnFail:
			TNT1 A 1
			TNT1 A 0
			Stop
	}
}

Actor OldBloodGib1 : OldBloodGib
{
	States
	{
		Spawn3:
			GIB1 A 0
			"####" "#" 0 A_Jump(256, "Spawn4")
	}
}

Actor OldBloodGib2 : OldBloodGib
{
	States
	{
		Spawn3:
			GIB2 A 0
			"####" "#" 0 A_Jump(256, "Spawn4")
	}
}

Actor OldBloodGib3 : OldBloodGib
{
	States
	{
		Spawn3:
			GIB3 A 0
			"####" "#" 0 A_Jump(256, "Spawn4")
	}
}

Actor OldBloodGibSpawner1
{
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Jump(256, "Gib1", "Gib2", "Gib3")
			Stop
		Gib1:
			TNT1 A 1
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("OldBloodGib1", frandom(-4.0, 4.0), 0.0, frandom(0.0, 360.0), CMF_AIMDIRECTION, frandom(0.0, 180.0))
			Stop
		Gib2:
			TNT1 A 1
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("OldBloodGib2", frandom(-4.0, 4.0), 0.0, frandom(0.0, 360.0), CMF_AIMDIRECTION, frandom(0.0, 180.0))
			Stop
		Gib3:
			TNT1 A 1
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("OldBloodGib3", frandom(-4.0, 4.0), 0.0, frandom(0.0, 360.0), CMF_AIMDIRECTION, frandom(0.0, 180.0))
			Stop
	}
}
