//===========================================================================
//
// OldBlood
// Written by Nash Muhandes
// License: MIT
//
//===========================================================================

Actor OldBloodGib
{
	Radius 2
	Height 8
	Gravity 0.43
	Speed 7
	Mass 1
	BounceType "Doom"
	BounceFactor 0.45
	BounceSound "nashgore/gibbounce"
	Decal "BloodSplat"
	+CLIENTSIDEONLY
	+MISSILE
	+FLOORCLIP
	+DONTSPLASH
	+NOTELEPORT
	+FORCEXYBILLBOARD
	+THRUACTORS

	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("OldBloodGibInit", 0)
			TNT1 A 0 A_Jump(128, 2)
			TNT1 A 1 A_Jump(256, "Spawn2")
			TNT1 A 1 A_Jump(256, "SpawnFail")
			Wait
		Spawn2:
			TNT1 A 0 A_Jump(128, 2)
			TNT1 A 0 A_SetScale(ScaleX * -1.0, ScaleY)
			TNT1 A 1 A_Jump(256, "Spawn3")
			Wait
		Spawn4:
			"####" ABCDEFGH 4
			Loop
		Death:
			"####" "#" 0 ACS_NamedExecuteWithResult("OldBloodAddToQueue", 0)
			// the FX is intentionally spawned in the ground and should disappear immediately, only generating the splashes as a result
			"####" "#" 0 A_SpawnItemEx("OldBloodFX1", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, SXF_TRANSFERTRANSLATION | SXF_CLIENTSIDE)
			"####" I 35
			"####" "#" 0 A_ChangeFlag("NoInteraction", 1)
			"####" "#" 0 A_ChangeFlag("MoveWithSector", 1)
			"####" "#" -1
			Stop
		SpawnFail:
			TNT1 A 1
			TNT1 A 0
			Stop
	}
}

Actor OldBloodGib1 : OldBloodGib
{
	States
	{
		Spawn3:
			GIB1 A 0
			"####" "#" 1 A_Jump(256, "Spawn4")
			Wait
	}
}

Actor OldBloodGib2 : OldBloodGib
{
	States
	{
		Spawn3:
			GIB2 A 0
			"####" "#" 1 A_Jump(256, "Spawn4")
			Wait
	}
}

Actor OldBloodGib3 : OldBloodGib
{
	States
	{
		Spawn3:
			GIB3 A 0
			"####" "#" 1 A_Jump(256, "Spawn4")
			Wait
	}
}

Actor OldBloodGib1Spawner
{
	+CLIENTSIDEONLY

	States
	{
		Spawn:
			TNT1 AAAAAAAAAA 0 NoDelay A_CustomMissile("OldBloodGib1", frandom[rnd_GibSpawner](-4.0, 4.0), 0.0, frandom[rnd_GibSpawner](0.0, 360.0), CMF_AIMDIRECTION, frandom[rnd_GibSpawner](0.0, 180.0))
			Stop
	}
}

Actor OldBloodGib2Spawner
{
	+CLIENTSIDEONLY

	States
	{
		Spawn:
			TNT1 AAAAAAAAAA 0 NoDelay A_CustomMissile("OldBloodGib2", frandom[rnd_GibSpawner](-4.0, 4.0), 0.0, frandom[rnd_GibSpawner](0.0, 360.0), CMF_AIMDIRECTION, frandom[rnd_GibSpawner](0.0, 180.0))
			Stop
	}
}

Actor OldBloodGib3Spawner
{
	+CLIENTSIDEONLY

	States
	{
		Spawn:
			TNT1 AAAAAAAAAA 0 NoDelay A_CustomMissile("OldBloodGib3", frandom[rnd_GibSpawner](-4.0, 4.0), 0.0, frandom[rnd_GibSpawner](0.0, 360.0), CMF_AIMDIRECTION, frandom[rnd_GibSpawner](0.0, 180.0))
			Stop
	}
}

Actor OldBloodGibSpawner
{
	+CLIENTSIDEONLY

	States
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_SpawnItemEx("OldBloodGib1Spawner", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, SXF_TRANSFERTRANSLATION | SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("OldBloodGib2Spawner", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, SXF_TRANSFERTRANSLATION | SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("OldBloodGib3Spawner", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, SXF_TRANSFERTRANSLATION | SXF_CLIENTSIDE)
			TNT1 AAAAAAAAAA 0 A_SpawnItemEx("OldBloodFX1",
				frandom[rnd_GibSpawnerBlood](-4.0, 4.0),
				frandom[rnd_GibSpawnerBlood](-4.0, 4.0),
				frandom[rnd_GibSpawnerBlood](-10.0, 10.0),
				frandom[rnd_GibSpawnerBlood](-2.0, 2.0),
				frandom[rnd_GibSpawnerBlood](-2.0, 2.0),
				frandom[rnd_GibSpawnerBlood](0.0, 2.0),
				frandom[rnd_GibSpawnerBlood](0.0, 360.0),
				SXF_TRANSFERTRANSLATION | SXF_CLIENTSIDE,
				100)
			Stop
	}
}
