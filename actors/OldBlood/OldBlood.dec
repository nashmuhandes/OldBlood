//===========================================================================
//
// OldBlood
// Written by Nash Muhandes
// License: MIT
//
//===========================================================================

Actor OldBlood : Blood replaces Blood
{
	+CORPSE
	+FORCEXYBILLBOARD
	-ALLOWPARTICLES

	States
	{
		Spawn:
			TNT1 AAA 0
			TNT1 A 0 A_SpawnItemEx("OldBloodFX1",
				frandom[rnd_SpawnBlood](-4.0, 4.0),
				frandom[rnd_SpawnBlood](-4.0, 4.0),
				frandom[rnd_SpawnBlood](-4.0, 4.0),
				frandom[rnd_SpawnBlood](-2.0, 2.0),
				frandom[rnd_SpawnBlood](-2.0, 2.0),
				frandom[rnd_SpawnBlood](0.0, 2.0),
				frandom[rnd_SpawnBlood](0.0, 360.0),
				SXF_TRANSFERTRANSLATION | SXF_CLIENTSIDE)
		BloodC:
			BLUD C 8 A_SetTics(random[rnd_BludTics](0, 8))
		BloodB:
			BLUD B 8 A_SetTics(random[rnd_BludTics](0, 8))
		BloodA:
			BLUD A 8
			Stop
		Crash:
			TNT1 A 0 A_SpawnItemEx("OldBloodSpot",
				frandom[rnd_SpawnBlood](-4.0, 4.0),
				frandom[rnd_SpawnBlood](-4.0, 4.0),
				0.0,
				0.0, 0.0, 0.0,
				frandom[rnd_SpawnBlood](0.0, 360.0),
				SXF_TRANSFERTRANSLATION | SXF_CLIENTSIDE)
			Stop
	}
}

Actor OldBloodSpot
{
	Radius 2
	Height 3
	+CLIENTSIDEONLY
	+DONTSPLASH
	+FLOORCLIP
	+FORCEXYBILLBOARD
	-SOLID

	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("OldBloodAddToQueue", 0)
			TNT1 A 0 A_SetScale(frandom[rnd_BloodSpot](0.5, 1.0), 1.0)
			TNT1 A 0 A_SetScale(ScaleX * frandom[rnd_BloodSpot](0.8, 1.0), ScaleY * frandom[rnd_BloodSpot](0.8, 1.0))
			TNT1 A 0 A_Jump(128, 2)
			TNT1 A 0 A_SetScale(ScaleX * -1.0, ScaleY)
			TNT1 A 1 A_Jump(256, "Blood1", "Blood2", "Blood3", "Blood4", "Blood5", "Blood6")
			Wait
		Blood1:
			VBLD A 35
			Goto BloodLoop
		Blood2:
			VBLD B 35
			Goto BloodLoop
		Blood3:
			VBLD B 35
			Goto BloodLoop
		Blood4:
			VBLD C 35
			Goto BloodLoop
		Blood5:
			VBLD D 35
			Goto BloodLoop
		Blood6:
			VBLD E 35
			Goto BloodLoop
		BloodLoop:
			"####" "#" 0 A_ChangeFlag("MoveWithSector", 1)
			"####" "#" 0 A_ChangeFlag("NoInteraction", 1)
			"####" "#" -1
			Stop
	}
}

Actor OldBloodFX1
{
	Gravity 0.11
	Scale 0.666
	+CLIENTSIDEONLY
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+NOCLIP
	+CORPSE

	States
	{
		Spawn:
			VBLD G 1 A_SetScale(ScaleX * 1.073, ScaleY * 1.025)
			VBLD G 1 A_FadeOut(0.045)
			Loop
		Crash:
			TNT1 A 0 A_ChangeFlag("NoInteraction", 1)
			TNT1 AAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("OldBloodSplash",
				frandom[rnd_SpawnBlood](-64.0, 64.0),
				frandom[rnd_SpawnBlood](-64.0, 64.0),
				0.0,
				0.0, 0.0, 0.0,
				0.0,
				SXF_TRANSFERTRANSLATION | SXF_CLIENTSIDE,
				179)
			Stop
	}
}

Actor OldBloodSplash
{
	Scale 0.612
	+CLIENTSIDEONLY
	+DONTSPLASH

	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SpawnItemEx("OldBloodSpot", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, SXF_TRANSFERTRANSLATION | SXF_CLIENTSIDE, 150)
			TNT1 A 0 A_PlaySound("nashgore/bloodsplash")
			BDRP BCDEF 3
			Stop
	}
}
